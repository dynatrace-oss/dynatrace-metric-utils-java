/**
 * Copyright 2021 Dynatrace LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file
 * except in compliance with the License. You may obtain a copy of the License at
 *
 *  http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software distributed under the
 * License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either
 * express or implied. See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'com.github.sherter.google-java-format' version '0.8'
}

group 'com.dynatrace.metric.util'
version = '1.2.0'

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    // Use JUnit test framework.
    testImplementation(platform('org.junit:junit-bom:5.7.0'))
    testImplementation('org.junit.jupiter:junit-jupiter')
    testImplementation("org.junit.jupiter:junit-jupiter-params:5.7.0")
    testImplementation("org.mockito:mockito-core:3.8.0")
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId 'dynatrace-metric-utils-java'

            from components.java

            pom {
                name = 'dynatrace-metric-utils-java'
                description = 'Utility for preparing communication with the Dynatrace v2 metrics API.'
                url = 'https://github.com/dynatrace-oss/dynatrace-metric-utils-java'

                scm {
                    connection = 'scm:git:git://github.com/dynatrace-oss/dynatrace-metric-utils-java.git'
                    developerConnection = 'scm:git:ssh://github.com:dynatrace-oss/dynatrace-metric-utils-java.git'
                    url = 'https://github.com/dynatrace-oss/dynatrace-metric-utils-java'
                }

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        name = 'Dynatrace Open Source Engineering'
                        email = 'opensource@dynatrace.com'
                        organization = 'Dynatrace LLC'
                        organizationUrl = 'https://www.dynatrace.com'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name 'sonatype'
            url 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            credentials {
                username findProperty('sonatypeUsername')
                password findProperty('sonatypePassword')
            }
        }
        mavenLocal()
    }
}

//signing {
//    def signingKey = findProperty('signingKey')
//    def signingPassword = findProperty('signingPassword')
//    useInMemoryPgpKeys(signingKey, signingPassword)
//    sign publishing.publications.mavenJava
//}
